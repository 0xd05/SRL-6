(*
Text
====

The text file holds functions and procedures that are used in Runescape text
recognition.

*)

(*
findText
~~~~~~~~

.. code-block:: pascal

    function findText(var x, y: integer; txt, fonts: TStringArray; b: TBox): boolean;

Returns true if the text (txt) of given fonts (fonts) are found in the specified
box (b).  If found, set's x and y to where the text was found. Option parameter
waitTime (default 0) will wait said ms for text to appear.


.. note::

    by Stupid3ooo, modified by LordJashin

Example:

.. code-block:: pascal

    var
      x, y: integer;
    begin
      if (findText(x, y, ['obby'], [upCharsEx], intToBox(MIX1, MIY1, MIX2, MIY2))) then
        writeln('Found exit to lobby text');
      .
      .
      .
    end;
*)
function findText(var x, y: integer; txt, fonts: TStringArray; b: TBox; waitTime: integer = 0): boolean;
var
  s, i, j: integer;
  t := (getSystemTime + waitTime);
begin
  while (getSystemTime < t) do
    for i := 0 to high(txt) do
      for j := 0 to high(fonts) do
      begin
        s := bitmapFromText(toStr(txt[i]), toStr(fonts[j]));

        if (b.x1 < 0) then b.x1 := 0;
        if (b.y1 < 0) then b.y1 := 0;

        result := findBitmapMaskTolerance(s, x, y, b.x1, b.y1, b.x2, b.y2, 30, 30);
        freeBitmap(s);

        if (result) then
          break(3);
      end;

  subDebug('findText('+toStr(txt)+'): '+toStr(result));
end;

(*
typeByte
~~~~~~~~

.. code-block:: pascal

    procedure typeByte(k: byte);

Types one character k.

.. note::

    by Mutant Squirrle

Example:

.. code-block:: pascal

    typeByte(VK_ENTER);

*)
procedure typeByte(k: byte);
begin
  {$IFDEF SMART}
  if (k = 13) then
    k := 10;
  {$ENDIF}

  KeyDown(k);
  Wait(10 + Random(50));
  KeyUp(k);
end;

(*
typeSend
~~~~~~~~

.. code-block:: pascal

    procedure typeSend(text: string; pressEnter: boolean = true);

Types text in a human-like way.  Optional parameter 'pressEnter'.  Will press
enter by default.

.. note::

    by SKy Scripter
    Last Updated: January 29th, 2013 by Coh3n

Example:

.. code-block:: pascal

    // to type and press enter
    typeSend('Hello');

    // to type and not press enter
    typeSend('Hello', false);

*)
procedure typeSend(text: string; pressEnter: boolean = true);
var
  i: Integer;
begin
  for i := 1 to Length(Text) do
  begin
    SendKeys(Text[i], 30 + Random(30), 30 + Random(30));
    Wait(40 + Random(40));
  end;

  if (pressEnter) then
    TypeByte(VK_ENTER);
end;
