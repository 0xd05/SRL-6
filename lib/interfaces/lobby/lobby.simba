(*
Lobby
=====

The lobby file holds functions and procedures that are used in the Runescape
lobby.

The source for this file can be found `here <https://github.com/SRL/SRL-6/blob/master/lib/interfaces/lobby/lobby.simba>`_.

*)

{$include_once ../interfaces.simba}
{$include_once ../options.simba}

{$f-}

(*
type TRSLobby
~~~~~~~~~~~~~

A type that stores functions and properties of the Runescape lobby.
*)
type
  TRSLobby = record(TRSInterface)
    __placeholder: byte;
  end;

(*
var lobby
~~~~~~~~~

Variable that stores functions and properties of the Runescape lobby.
*)
var
  lobby: TRSLobby;

(*
const Lobby Tabs
~~~~~~~~~~~~~~~~

Integer constants that representing the different lobby tabs.

Example:

.. code-block:: pascal

  if (lobby.getTab() = LOBBY_CLAN) then
    writeln('The clan chat tab is open!');

*)
const
  LOBBY_COUNT = 6;
    LOBBY_PLAYER = 0;
    LOBBY_WORLDS = 1;
    LOBBY_FRIENDS = 2;
    LOBBY_CHAT = 3;
    LOBBY_CLAN = 4;
    LOBBY_OPTIONS = 5;

(*
const Internal
~~~~~~~~~~~~~~

Internal lobby constants.
*)
const
  // used in getCurrentTab and _openTab calulations
  __LOBBY_TAB_WIDTH    = 102;
  __LOBBY_TAB_OFFSET_X = 7;  // x/y is the midpoint of the left border of the first tab
  __LOBBY_TAB_OFFSET_Y = 21;

{*
TRSLobby.__init()
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TRSLobby.__init();

Initiates the lobby default values. Called at the end of this file.

.. note::

    - by Coh3n
    - Last updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    lobby.__init();
*}
procedure TRSLobby.__init();
begin
  with self do
  begin
    name := 'RS Lobby';
    ID := ID_INTERFACE_LOBBY;
    parentID := -1;
    static := true;
    x1 := 153;
    y1 := 82;
    x2 := 805;
    y2 := 556;
    w := (x2 - x1);
    h := (y2 - y1);
    cx := self.getCenterPoint().x;
    cy := self.getCenterPoint().y;
  end;
end;

{*
TRSLobby.__skipEmailScreen()
~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure TRSLobby.__skipEmailScreen();

Handles the validate email screen that comes up when you enter to lobby.

.. note::

    - by NCDS & Coh3n
    - Last updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    lobby.__skipEmailScreen();
*}
procedure TRSLobby.__skipEmailScreen();
var
  tpa: TPointArray;
  wid, hei: integer;
begin
  getClientDimensions(wid, hei);

  findColors(tpa, 14733486, 0, 0, wid-1, hei-1); // color of the "Skip" text
  if (length(tpa) = 43) then
    mouseBox(tpa.getBounds(), MOUSE_LEFT);
end;

{*
TRSLobby.__find()
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby.__find(): boolean;

Finds and sets the bounds on the lobby interface. This allows the lobby to be
anywhere on the screen.  This function is called whenever TRSLobby.isOpen() is
called.

.. note::

    - by Coh3n
    - Last Updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    lobby.__find();
*}
function TRSLobby.__find(): boolean;
var
  wid, hei: integer;
  tpa: TPointArray;
begin
  getClientDimensions(wid, hei);

  findColors(tpa, 11119274, 0, 0, wid-1, hei-1); // white border of the lobby

  if (length(tpa) > 0) then
  begin
    result := true;
    self.setBounds(tpa.getBounds());
    //smartImage.drawBox(self.getBounds(), false);
  end else
    print('TRSLobby.findBounds(): Failed to find lobby bounds', TDebug.ERROR);
end;

(*
TRSLobby.isOpen()
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby.isOpen(): boolean;

Returns true if we are at the lobby screen is loaded. Uses text recognition of
the tabs.

.. note::

    - by Coh3n
    - Last Updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    while (not lobby.isOpen()) do
      wait(100 + Random(400));

*)
function TRSLobby.isOpen(): boolean;
var
  wid, hei: integer;
  dummy: TPoint;
begin
  self.__skipEmailScreen(); // just in case
  getClientDimensions(wid, hei);

  if (findText(dummy, ['World'], ['UpChars'], intToBox(0, 0, wid-1, hei-1)) and
      findText(dummy, ['Options'], ['UpChars'], intToBox(0, 0, wid-1, hei-1))) then
  begin
    result := true;
    self.__find();
  end;
end;

(*
TRSLobby.findPlayButton()
~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function findPlayButton(var res: TPoint): boolean;

Returns true if the play button is found somewhere on the client.  Used as an easier
way to detect the lobby screen since some lobby tabs are different sizes, causing
lobby interface to change.

.. note::

    - by Coh3n
    - Last updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    lobby.findPlayButton(p);
*)
function TRSLobby.findPlayButton(var res: TPoint): boolean;
var
  tpa: TPointArray;
  width, height: integer;
begin
  getClientDimensions(width, height);

  if (findColors(tpa, 12235426, 0, 0, width - 1, height - 1)) then // top border of the Play button
    if (length(tpa) = 247) then
    begin
      result := true;
      res := tpa.getMiddle();
      res.offset(point(0, 10));
    end;
end;

(*
TRSLobby.getCurrentTab()
~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby.getCurrentTab: integer;

Gets current lobby tab.

.. note::

    - by Bionicle1800, NCDS
    - Last Updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    if (lobby.getCurrentTab() = LOBBY_WORLDS) then
      writeln('The world list is open!');

*)
function TRSLobby.getCurrentTab(): integer;
begin
  if (not self.isOpen()) then
    exit(-1);

  for result := 0 to (LOBBY_COUNT - 1) do
    // color of an active tab's border
    if (getColor((self.x1 + __LOBBY_TAB_OFFSET_X) + (result * __LOBBY_TAB_WIDTH), (self.y1 + __LOBBY_TAB_OFFSET_Y)) = 7831173) then
    begin
      print('TRSLobby.getCurrentTab(): Current tab: '+toStr(result), TDebug.SUB);
      exit;
    end;

  Result := -1;
end;

{*
TRSLobby._open()
~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby._open(): boolean;

Exits from the game to the lobby screen. Should call players[currentPlayer].exitToLobby()
instead of this function.

.. note::

    - by Starblaster100, Raymond, IceFire908, Tarajunky, & Coh3n
    - Last Updated: 23 July 2013 by Co3n

Example:

.. code-block:: pascal

    if (players[currentPlayer].exitToLobby()) then
      writeln('We''ve exited to the lobby!');
*}
function TRSLobby._open(): boolean;
var
  t: integer;
  p: TPoint;
  b: TBox;
begin
  if (lobby.isOpen()) then
    exit(true);

  if (options.open()) then
  begin
    p := point(options.x1 + round(options.w / 2), options.y2 - 45); // the exit to lobby button
    mouse(p.rand(randomRange(-50, 50), 5), MOUSE_LEFT);

    t := (getSystemTime() + (7000));

    while (not result) and (getSystemTime() < t) do
    begin
      print('Waiting for lobby screen...', TDebug.SUB);
      result := lobby.isOpen();
      wait(700 + random(300));
    end;
  end;
end;

{*
TRSLobby._openTab
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby._openTab(tab: integer): boolean;

Opens the lobby tab specified by 'tab'. Valied arguments for 'tab' are the
lobby tab constants at the top of the file. Is an internal SRL function, should
use the lobby file's variables instead.

.. note::

    - by Bionicle1800 & Coh3n
    - Last Updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    lobby._openTab(LOBBY_WORLDS);

*}
function TRSLobby._openTab(tab: integer): boolean;
var
  p: TPoint;
  currTab: integer;
begin
  if (not self.isOpen()) then
    exit(false);

  currTab := self.getCurrentTab();

  if (currTab = tab) then
    exit(true);

  p := point(self.x1 + __LOBBY_TAB_OFFSET_X + (tab * __LOBBY_TAB_WIDTH), self.y1 + __LOBBY_TAB_OFFSET_Y);
  mouse(p.rand(randomRange(20, 90), randomRange(-10, 10)), MOUSE_LEFT);
  wait(200 + random(200));
  result := true;

  if (not result) then
    print('TRSLobby._openTab(): Failed to open tab '+toStr(tab), TDebug.ERROR);
end;

{*
TRSLobby._leave()
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TRSLobby._leave(): boolean;

Leaves the lobby screen. This instance of the function is internal.  Use
players[currentPlayer].logout();.

.. note::

    - by IceFire908
    - Last Updated: 23 July 2013 by Coh3n

Example:

.. code-block:: pascal

    if (lobby.isOpen()) then
      if (not players[currentPlayer].leave()) then
        writeln('Oh no, we didn''t make it out of the Lobby Screen.');
*}
function TRSLobby._leave(): boolean;
var
  t: integer;
  exitButton: TPoint;
begin
  result := (not lobby.isOpen());

  t := (getSystemTime() + 30000);
  exitButton := point(self.x2 - 20, self.y1 + 20).rand(3);

  while (not result) and (getSystemTime() < t) do
  begin
    mouse(exitButton, MOUSE_LEFT);
    print('Waiting for login screen...', TDebug.SUB);
    wait(randomRange(500, 1000));
    result := (not lobby.isOpen());
  end;
end;

begin
  lobby.__init();
end;
