(*
Lobby
=====

The lobby file holds functions and procedures that are used in the Runescape
lobby.

*)

{$include_once gametabs/logout.simba}

(*
Constant: Lobby
~~~~~~~~~~~~~~~

Integer constants that representing the different lobby tabs.

Example:

.. code-block:: pascal

  if (GetlobbyScreen() = LOBBY_CLAN) then
    writeln('The clan chat tab is open!');

*)
const
  LOBBY_COUNT = 6;
    LOBBY_PLAYER = 0;
    LOBBY_WORLD = 1;
    LOBBY_FRIENDS = 2;
    LOBBY_CHAT = 3;
    LOBBY_CLAN = 4;
    LOBBY_OPTIONS = 5;

{*
__validateEmailScreen()
~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    procedure __validateEmailScreen();

Handles the validate email screen that comes up when you enter to lobby.

.. note::

    by NCDS
    Last updated: Mar. 1, 2013 by Coh3n

Example:

.. code-block:: pascal

    __validateEmailScreen();
*}
procedure __validateEmailScreen();
begin
  if (getColor(473, 468) = 1001495) then // gold border around the "confirm" button
    mouseBox(intToBox(502, 474, 548, 485), MOUSE_LEFT); // the "skip" button
end;

(*
getLobbyTab
~~~~~~~~~~~

.. code-block:: pascal

    function getLobbyTab: integer;

Gets current lobby tab.

.. note::

    by Bionicle1800, NCDS

Example:

.. code-block:: pascal

    if (getLobbyTab = LOBBY_WORLD) then
      writeln('The world list is open!');

*)
function getLobbyTab: integer;
begin
  for result := 0 to (LOBBY_COUNT - 1) do
    if (GetColor(63 + result * 102, 82) = 11582147) then
      exit;

  Result := -1;
end;

(*
isLobbyScreenOpen
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function isLobbyScreenOpen: boolean;

Returns true if we are at the lobby screen is loaded.

.. note::

    by IceFire908 & Bionicle1800

Example:

.. code-block:: pascal

    while (not isLobbyScreenOpen) do
      wait(100 + Random(400));

*)
function isLobbyScreenOpen(): boolean;
begin
  __validateEmailScreen();
  wait(200 + random(200));
  result := inRange(getLobbyTab, LOBBY_PLAYER, LOBBY_OPTIONS);
end;

{*
_exitToLobby
~~~~~~~~~~~~

.. code-block:: pascal

    function _exitToLobby(): boolean;

Exits to the lobby screen. Should call players[currentPlayer].exitToLobby()
instead of this function.

.. note::

    by Starblaster100, Raymond, IceFire908, Tarajunky, & Coh3n
    Last Updated: Jan. 10th, by Co3n

Example:

.. code-block:: pascal

    if (_exitToLobby()) then
      writeln('We''ve exited to the lobby!');
*}
function _exitToLobby(): boolean;
var
  t := (getSystemTime + (15000));
begin
  if (isLobbyScreenOpen()) then
  begin
    result := true;
    exit;
  end;

  if (not tabLogout.open()) then
    exit;

  wait(100 + random(200));

  if (tabLogout.clickText(['Lobby'])) then
    while (not result) and (getSystemTime < t) do
    begin
      debug('Waiting for lobby screen...');
      result := isLobbyScreenOpen();
      wait(700 + random(500));
    end;
end;

(*
leaveLobby
~~~~~~~~~~

.. code-block:: pascal

    function leaveLobby(): boolean;

Leaves the lobby screen.

.. note::

    by IceFire908
    Last Updated: Jan. 7th, 2013 by Coh3n

Example:

.. code-block:: pascal

    if (isLobbyScreenOpen()) then
      if (not leaveLobby()) then
        writeln('Oh no, we didn''t make it out of the Lobby Screen.');
*)
function leaveLobby(): boolean;
var
  t := (getSystemTime() + 30000);
  exitButton := point(690, 85).rand(3);
begin
  addHeader('leaveLobby(): leaving lobby');

  result := (not isLobbyScreenOpen());

  while (not result) and (getSystemTime() < t) do
  begin
    debug('Waiting for login screen...');
    mouse(exitButton, MOUSE_LEFT);
    wait(randomRange(1000, 2000));
    result := (not isLobbyScreenOpen());
  end;

  closeHeader('leaveLobby(): ' + toStr(result));
end;
