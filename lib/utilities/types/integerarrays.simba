(*
Integer Arrays
==============

A file that stores type functions to manipulate TIntegerArrays.

The source for this file can be found `here <https://github.com/SRL/SRL-6/blob/master/lib/utilities/types/integerarrays.simba>`_.

Contributers:
  - Zyt3x
  - Dgby714

*)

{$f-}

(*
TIntegerArray.sum
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TIntegerArray.sum() : Integer;

Returns the sum of all values in a TIntegerArray.

Example:

.. code-block:: pascal

    writeLn(toStr(TIA.sum()));

*)
function TIntegerArray.sum() : Integer;
begin
  result := sumIntegerArray(self);
end;

(*
TIntegerArray.sum64
~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TIntegerArray.sum64() : Integer;

Returns the sum of all values in a TIntegerArray.  Returns the value in a Int64 variable.

.. note::

    Used for large numbers.

Example:

.. code-block:: pascal

    writeLn(toStr(TIA.sum64()));

*)
function TIntegerArray.sum64() : Int64;
begin
  result := sum64IntArr(self);
end;

(*
TIntegerArray.average
~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TIntegerArray.average() : Integer;

Returns the average of all values in a TIntegerArray.

Example:

.. code-block:: pascal

    writeLn(toStr(TIA.average()));

*)
function TIntegerArray.average() : Integer;
begin
  result := averageTIA(self);
end;

(*
TIntegerArray.sortQuick
~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TIntegerArray.sortQuick(const sortSelf : Boolean = True) : TIntegerArray;

Used to QuickSort the TIntegerArray.

Will only result the sorted array if sortSelf is False.

Example:

.. code-block:: pascal

    TIA.sortQuick();
    sorted := TIA.sortQuick(False);

*)
function TIntegerArray.sortQuick(const sortSelf : Boolean = True) : TIntegerArray;
begin
  result := copy(self);
  quickSort(result);
  if sortSelf then
    self := result;
end;

(*
TIntegerArray.toExtendedArray
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TIntegerArray.toExtendedArray() : TExtendedArray;

Turns a TIntegerArray into a TExtendedArray.

Example:

.. code-block:: pascal

    TEA := TIA.toExtendedArray();

*)
function TIntegerArray.toExtendedArray() : TExtendedArray;
var
  I : Integer;
begin
  setLength(result, length(self));
  for I := 0 to high(self) do
    result[I] := Extended(self[I]);
end;

(*
TIntegerArray.toByteArray
~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TIntegerArray.toByteArray() : TByteArray;

Turns a TIntegerArray into a TByteArray.

Example:

.. code-block:: pascal

    TEA := TIA.toByteArray();

*)
function TIntegerArray.toByteArray() : TByteArray;
var
  I : Integer;
begin
  setLength(result, length(self));
  for I := 0 to high(self) do
    result[I] := Byte(max(0, min(255, self[I])));
end;

(*
TIntegerArray.getMostCommon
~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function TIntegerArray.getMostCommon(): LongInt;

Returns the most common number in the array, useful for finding the most common
color in a TPointArray.

.. note::

    - by Dgby714

Example:

.. code-block:: pascal

     mostCol := getColors(tpa).getMostCommon();

*)
function TIntegerArray.getMostCommon(): LongInt;
var
  H, I, Cur, Count, HighCount: LongInt;
begin
  QuickSort(self);

  Cur := self[0] - 1;
  Count := 0;
  HighCount := 0;

  H := High(self);
  for I := 0 to H do
  begin
    if (Cur <> self[I]) then
    begin
      if (Count > HighCount) then
      begin
        HighCount := Count;
        Result := Cur;
      end;

      Count := 0;
      Cur := self[I];
    end;

    Inc(Count);
  end;
end;

{$f+}
